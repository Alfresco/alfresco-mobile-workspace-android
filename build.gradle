import com.alfresco.buildsrc.Libs

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin

        classpath Libs.Google.servicesPlugin
        classpath Libs.Google.Firebase.crashlyticsPlugin

        classpath Libs.spotless
        classpath Libs.gradleVersions
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven { url "https://artifacts.alfresco.com/nexus/content/groups/public" }
        maven { url "https://artifacts.alfresco.com/nexus/content/groups/public-snapshots" }
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    afterEvaluate {
        if(it.hasProperty('android')) {
            android {
                compileSdkVersion 29

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 29
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                sourceSets['main'].java.srcDir('src/main/kotlin')

                // Force kotlin-reflect version until Moshi is updated
                configurations.all {
                    resolutionStrategy {
                        force Libs.Kotlin.reflect
                    }
                }
            }
        }
    }

    apply from: "$project.rootDir/config/spotless.gradle"

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            // allWarningsAsErrors = true

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
            freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"

            // Set JVM target to 1.8
            jvmTarget = "1.8"
        }
    }
}

apply from: "$rootDir/config/versions.gradle"
