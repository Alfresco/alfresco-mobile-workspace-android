import com.alfresco.buildsrc.Libs

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '4.0.1'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

def isNonStable = { String version ->
    def regex = /^[0-9,.v-]+$/
    return !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && project.hasProperty('stable')
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven { url "https://artifacts.alfresco.com/nexus/content/groups/public" }
        maven { url "https://artifacts.alfresco.com/nexus/content/groups/public-snapshots" }
        google()
        jcenter()
        mavenCentral()
    }
}

subprojects {
    afterEvaluate {
        if(it.hasProperty('android')) {
            android {
                compileSdkVersion 29

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 29
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                sourceSets['main'].java.srcDir('src/main/kotlin')
            }
        }
    }

    apply from: "$project.rootDir/config/spotless.gradle"

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            // allWarningsAsErrors = true

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
            freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"

            // Set JVM target to 1.8
            jvmTarget = "1.8"
        }
    }
}
